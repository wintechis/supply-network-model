PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/sn/vocab#>
PREFIX esn: <https://example.org/esn/vocab#>
PREFIX csn: <https://example.org/csn/vocab#>
PREFIX osn: <https://example.org/osn/vocab#>

CONSTRUCT {
    ?B1 a csn:Country ;
        rdfs:label ?B1_label ;
        csn:inContinent ?C1 .
    ?C1 rdfs:label ?C1_label .
    ?B2 a csn:Country ;
        rdfs:label ?B2_label ;
        csn:inContinent ?C2 .
    ?C2 rdfs:label ?C2_label .
    ?relationB a csn:Trade ;
        sn:from ?B1 ; sn:into ?B2 .
    _:BTV a sn:Volume ;
        sn:relation ?relationB ;
        sn:time ?time ;
        sn:unit ?unit ;
        sn:quantity ?aggregatedQuantity .
} WHERE {
    { SELECT ?C1 ?C1_label ?C2 ?C2_label ?B1 ?B1_label ?B2 ?B2_label ?relationB ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
        WHERE {
            # infer network
            ?A1 a esn:Enterprise ;
                esn:registeredIn ?B1 .
            ?A2 a esn:Enterprise ;
                esn:registeredIn ?B2 .
            ?AR a esn:Trade ;
                sn:from ?A1 ; sn:into ?A2 .
            ?ARV a sn:Volume ;
                sn:relation ?AR ;
                sn:time ?time ;
                sn:unit ?unit ;
                sn:quantity ?quantity .
            ?B1 rdfs:label ?B1_label .
            ?B2 rdfs:label ?B2_label .
	        BIND ( IRI(CONCAT(STR(?B1),ENCODE_FOR_URI(?B2_label))) AS ?relationB ) .

            # pass on associated entities
            OPTIONAL {
                ?A1 esn:continent ?C1 .
                ?C1 rdfs:label ?C1_label .
                ?A2 esn:continent ?C2 .
                ?C2 rdfs:label ?C2_label .
            }
        } GROUP BY ?C1 ?C1_label ?C2 ?C2_label ?B1 ?B1_label ?B2 ?B2_label ?relationB ?time ?unit } }
