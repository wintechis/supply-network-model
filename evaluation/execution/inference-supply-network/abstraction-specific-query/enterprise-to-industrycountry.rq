PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/sn/vocab#>
PREFIX esn: <https://example.org/esn/vocab#>
PREFIX csn: <https://example.org/csn/vocab#>
PREFIX isn: <https://example.org/isn/vocab#>
PREFIX osn: <https://example.org/osn/vocab#>

CONSTRUCT {
    ?countryindustry1 a sn:MultidimensionalEntity ;
        sn:dimension ?country1 , ?industry1 .
    ?country1 a csn:Country ;
        rdfs:label ?country1_label ;
        csn:inContinent ?continent1 .
    ?industry1 a isn:Industry ;
        rdfs:label ?industry1_label .
    ?countryindustry2 a sn:MultidimensionalEntity ;
        sn:dimension ?country2 , ?industry2 .
    ?country2 a csn:Country ;
        rdfs:label ?country2_label ;
        csn:inContinent ?continent2 .
    ?industry2 a isn:Industry ;
        rdfs:label ?industry2_label .
    ?relation a sn:Relation ;
        sn:from ?countryindustry1 ; sn:into ?countryindustry2 .
    ?volume a sn:Volume ;
        sn:relation ?relation ;
        sn:time ?time ;
        sn:unit ?unit ;
        sn:quantity ?aggregatedQuantity .
    ?continent1 rdfs:label ?continent1_label .
    ?continent2 rdfs:label ?continent2_label .
} WHERE {
    { SELECT ?continent1 ?continent1_label ?continent2 ?continent2_label ?industry1 ?industry1_label ?industry2 ?industry2_label ?country1 ?country1_label ?country2 ?country2_label ?countryindustry2 ?countryindustry1 ?relation ?volume ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
        WHERE {
            # infer network
            ?enterprise1 a esn:Enterprise ;
                esn:registeredIn ?country1 ;
                esn:industry ?industry1 .
            ?enterprise2 a esn:Enterprise ;
                esn:registeredIn ?country2 ;
                esn:industry ?industry2 .
            ?enterprisetrade a esn:Trade ;
                sn:from ?enterprise1; sn:into ?enterprise2 .
            ?enterprisetradevolume a sn:Volume ;
                sn:relation ?enterprisetrade ;
                sn:time ?time ;
                sn:unit ?unit ;
                sn:quantity ?quantity .
            ?country1 rdfs:label ?country1_label .
            ?country2 rdfs:label ?country2_label .
            ?industry1 rdfs:label ?industry1_label .
            ?industry2 rdfs:label ?industry2_label .
	        BIND ( IRI(CONCAT(STR(?country1),"_",ENCODE_FOR_URI(?industry2_label),"_relation")) AS ?relation ) .
	        BIND ( IRI(CONCAT(STR(?country1),"_",ENCODE_FOR_URI(?industry1_label))) AS ?countryindustry1 ) .
	        BIND ( IRI(CONCAT(STR(?country2),"_",ENCODE_FOR_URI(?industry2_label))) AS ?countryindustry2 ) .
	        BIND ( IRI(CONCAT(STR(?time),"_",ENCODE_FOR_URI(?country1_label),"_",ENCODE_FOR_URI(?industry2_label))) AS ?volume ) .

            # pass on associated entities
            OPTIONAL {
                ?enterprise1 esn:continent ?continent1 .
                ?continent1 rdfs:label ?continent1_label .
                ?enterprise2 esn:continent ?continent2 .
                ?continent2 rdfs:label ?continent2_label .
            }
        } GROUP BY ?continent1 ?continent1_label ?continent2 ?continent2_label ?industry1 ?industry1_label ?industry2 ?industry2_label ?country1 ?country1_label ?country2 ?country2_label ?countryindustry2 ?countryindustry1 ?relation ?volume ?time ?unit } }
