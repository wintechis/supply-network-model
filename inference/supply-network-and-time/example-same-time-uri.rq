PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/supply-network/onto#>
PREFIX csn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/csn-ontology#>
PREFIX esn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/esn-ontology#>

CONSTRUCT {
    ?c1 a csn:Country .
    ?c2 a csn:Country .
    _:CT a csn:Trade ;
        sn:from ?c1 ; sn:into ?c2 .
    _:CTV a sn:Volume ;
        sn:relation _:CT ;
        sn:time ?time ;
        sn:unit ?unit ;
        sn:quantity ?aggregatedQuantity .
} WHERE {
    { SELECT ?c1 ?c2 ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
        WHERE {
            esn:registeredIn rdfs:subPropertyOf sn:associatedEntity .
            ?e1 a esn:Enterprise ;
                esn:registeredIn ?c1 .
            ?e2 a esn:Enterprise ;
                esn:registeredIn ?c2 .
            ?ET a esn:Trade ;
                sn:from ?e1 ; sn:into ?e2 .
            ?ETV a sn:Volume ;
                sn:relation ?ET ;
                sn:time ?time ;
                sn:unit ?unit ;
                sn:quantity ?quantity .
        } GROUP BY ?c1 ?c2 ?time ?unit } }