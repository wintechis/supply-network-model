BASE <https://purl.org/supply-network/any-data/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/supply-network/onto#>
PREFIX snd: <https://purl.org/supply-network/any-data/>
PREFIX csn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/csn-ontology#>
PREFIX esn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/esn-ontology#>

CONSTRUCT {
    ?target_supplyflow a sn:SupplyFlow ;
        sn:from [ csn:country ?target_from_entity ; ] ;
        sn:into [ csn:country ?target_into_entity ; ] ;
        sn:volume ?target_volume .
    ?target_volume a sn:Volume ;
        sn:time ?time ;
        sn:quantity ?aggregatedQuantity ;
        sn:unit ?unit .
} WHERE {
    { SELECT ?target_from_entity ?target_into_entity
        ?target_supplyflow ?target_volume ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
    WHERE {
        ?source_supplyflow a sn:SupplyFlow ;
            sn:from/csn:country ?target_from_entity ;
            sn:into/csn:country ?target_into_entity ;
            sn:volume ?source_volume .
        ?source_volume a sn:Volume ;
            sn:time ?time ;
            sn:quantity ?quantity ;
            sn:unit ?unit .
        BIND(IRI(MD5(CONCAT(STR(?target_from_entity),
            STR(?target_into_entity)))) AS ?target_supplyflow) .
        BIND(IRI(MD5(CONCAT(STR(?target_supplyflow),
            STR(?time),STR(?unit)))) AS ?target_volume) .
    } GROUP BY ?target_from_entity ?target_into_entity
        ?target_supplyflow ?target_volume ?time ?unit } }

# industry fällt ersatzlos weg, wird nicht aggregiert
# enterprise wird aggregiert zu country, enterprise fällt also weg, country bleibt
# continent wird kopiert, da von country ableitbar
#
# alle triple von :from/:into BN kopieren, ohne industry 
# nur continent triple von :from/:into BN kopieren
#
# kopatibel mit QB, wenn :from/:into dimension property
# inkompatibel mit QB4OLAP, weil :from/:into nicht in hierarchie
#
# falls Inferenz mit entsprechenden Regeln angewendet wird, wird continent automatisch übertragen
# steht beim Land der Kontinent dabei, kann nach jedem Aggregieren wieder continent zur observation geschrieben werden