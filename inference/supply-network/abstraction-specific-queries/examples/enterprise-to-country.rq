PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/sn/vocab#>
PREFIX esn: <https://example.org/esn/vocab#>
PREFIX csn: <https://example.org/csn/vocab#>

CONSTRUCT {
    ?target_supplyNetwork a sn:SupplyNetwork ;
        sn:structure csn:CountryDataStructure ;
        sn:supplyFlow ?target_supplyflow ;
        sn:inferredFrom ?source_supplyNetwork .
    ?target_from_entity a csn:Country ;
        rdfs:label ?target_from_entitylabel .
    ?target_into_entity a csn:Country ;
        rdfs:label ?target_into_entitylabel .
    ?target_supplyflow a sn:SupplyFlow ;
        sn:supplyFlowStructure csn:CountrySupplyFlowKey ;
        csn:export ?target_from_entity ;
        csn:import ?target_into_entity ;
        sn:volume ?target_volume .
    ?target_volume a sn:Volume ;
        sn:time ?time ;
        sn:quantity ?aggregatedQuantity ;
        sn:unit ?unit .
} WHERE {
    { SELECT ?target_into_entity ?target_into_entitylabel ?target_from_entity ?target_from_entitylabel
    ?target_supplyNetwork ?source_supplyNetwork
    ?target_supplyflow ?target_volume
    ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
    WHERE {
        ?source_supplyNetwork a sn:SupplyNetwork ;
            sn:structure esn:EnterpriseDataStructure ;
            sn:supplyFlow ?source_supplyflow .
        ?source_from_entity a esn:Enterprise ;
            esn:registeredInCountry ?target_from_entity .
        ?source_into_entity a esn:Enterprise ;
            esn:registeredInCountry ?target_into_entity .
        ?source_supplyflow a sn:SupplyFlow ;
            sn:supplyFlowStructure esn:EnterpriseSupplyFlowKey ;
            esn:supplier ?source_from_entity ;
            esn:customer ?source_into_entity ;
            sn:volume ?source_volume .
        ?source_volume a sn:Volume ;
            sn:time ?time ;
            sn:quantity ?quantity ;
            sn:unit ?unit .
        OPTIONAL { ?target_from_entity rdfs:label ?target_from_entitylabel . }
        OPTIONAL { ?target_into_entity rdfs:label ?target_into_entitylabel . }
        BIND(IRI(CONCAT("https://www.example.org/",MD5(CONCAT(STR(?source_supplyNetwork),
            STR(csn:CountryDataStructure))))) AS ?target_supplyNetwork) .
        BIND(IRI(CONCAT("https://www.example.org/",MD5(CONCAT(STR(?target_from_entity),
            STR(?target_into_entity))))) AS ?target_supplyflow) .
        BIND(IRI(CONCAT("https://www.example.org/",MD5(CONCAT(STR(?target_supplyflow),
            STR(?time),STR(?unit))))) AS ?target_volume) .
    } GROUP BY ?target_into_entity ?target_into_entitylabel ?target_from_entity ?target_from_entitylabel
        ?target_supplyNetwork ?source_supplyNetwork
        ?target_supplyflow ?target_volume
        ?time ?unit } }
