BASE <https://purl.org/supply-network/any-data/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/supply-network/onto#>
PREFIX snd: <https://purl.org/supply-network/any-data/>
PREFIX csn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/csn-ontology#>
PREFIX esn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/esn-ontology#>
PREFIX isn: <https://paul.ti.rw.fau.de/~ju32haqi/supply-networks/abstraction-examples/isn-ontology#>

CONSTRUCT {
    ?target_supplyflow a sn:SupplyFlow ;
        csn:export ?target_from_entity ;
        csn:import ?target_into_entity ;
        isn:supply ?target_from_industry ;
        isn:demand ?target_into_industry ;
        sn:volume ?target_volume .
    ?target_volume a sn:Volume ;
        sn:time ?time ;
        sn:quantity ?aggregatedQuantity ;
        sn:unit ?unit .
} WHERE {
    { SELECT ?target_from_entity ?target_into_entity ?target_from_industry ?target_into_industry
        ?target_supplyflow ?target_volume ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
    WHERE {
        ?source_supplyflow a sn:SupplyFlow ;
            csn:export ?target_from_entity ;
            csn:import ?target_into_entity ;
            isn:supply ?target_from_industry ;
            isn:demand ?target_into_industry ;
            sn:volume ?source_volume .
        ?source_volume a sn:Volume ;
            sn:time ?time ;
            sn:quantity ?quantity ;
            sn:unit ?unit .
        BIND(IRI(MD5(CONCAT(STR(?target_from_entity),STR(?target_from_industry),STR(?target_into_industry),
            STR(?target_into_entity)))) AS ?target_supplyflow) .
        BIND(IRI(MD5(CONCAT(STR(?target_supplyflow),
            STR(?time),STR(?unit)))) AS ?target_volume) .
    } GROUP BY ?target_from_entity ?target_into_entity ?target_from_industry ?target_into_industry
        ?target_supplyflow ?target_volume ?time ?unit } }
