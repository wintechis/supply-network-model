PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/sn/vocab#>
PREFIX esn: <https://example.org/esn/vocab#>
PREFIX csn: <https://example.org/csn/vocab#>
PREFIX osn: <https://example.org/osn/vocab#>

CONSTRUCT {
    ?country1 a csn:Country ;
        rdfs:label ?country1_label ;
        csn:inContinent ?continent1 .
    ?continent1 rdfs:label ?continent1_label .
    ?country2 a csn:Country ;
        rdfs:label ?country2_label ;
        csn:inContinent ?continent2 .
    ?continent2 rdfs:label ?continent2_label .
    ?relationB a csn:Trade ;
        sn:from ?country1 ; sn:into ?country2 .
    _:BTV a sn:Volume ;
        sn:relation ?relationB ;
        sn:time ?time ;
        sn:unit ?unit ;
        sn:quantity ?aggregatedQuantity .
} WHERE {
    { SELECT ?continent1 ?continent1_label ?continent2 ?continent2_label ?country1 ?country1_label ?country2 ?country2_label ?relationB ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
        WHERE {
            # infer network
            ?enterprise1 a esn:Enterprise ;
                esn:registeredIn ?country1 .
            ?enterprise2 a esn:Enterprise ;
                esn:registeredIn ?country2 .
            ?enterprisetrade a esn:Trade ;
                sn:from ?enterprise1 ; sn:into ?enterprise2 .
            ?enterprisetradevolume a sn:Volume ;
                sn:relation ?enterprisetrade ;
                sn:time ?time ;
                sn:unit ?unit ;
                sn:quantity ?quantity .
            ?country1 rdfs:label ?country1_label .
            ?country2 rdfs:label ?country2_label .
	        BIND ( IRI(CONCAT(STR(?country1),"_",ENCODE_FOR_URI(?country2_label))) AS ?relationB ) .

            # pass on associated entities
            OPTIONAL {
                ?enterprise1 esn:continent ?continent1 .
                ?continent1 rdfs:label ?continent1_label .
                ?enterprise2 esn:continent ?continent2 .
                ?continent2 rdfs:label ?continent2_label .
            }
        } GROUP BY ?continent1 ?continent1_label ?continent2 ?continent2_label ?country1 ?country1_label ?country2 ?country2_label ?relationB ?time ?unit } }
