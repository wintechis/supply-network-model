PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sn: <https://purl.org/sn/vocab#>
PREFIX qb:    <http://purl.org/linked-data/cube#>

CONSTRUCT {
    # resulting supply network
    ?target_supplyNetwork a sn:SupplyNetwork ;
        sn:structure ?target_supplyNetworkStructure ;
        sn:supplyLink ?target_supplylink .

    # resulting entities
    ?target_from_entity a ?target_class ;
        rdfs:label ?target_from_entitylabel .
        # ?associatedEntity_property ?associatedEntity_from_entity .
    ?target_into_entity a ?target_class ;
        rdfs:label ?target_into_entitylabel .
        # ?associatedEntity_property ?associatedEntity_into_entity .

    # resulting supply links
    ?target_supplylink a sn:SupplyLink ;
        qb:sliceStructure ?target_supplyLinkKey ;
        ?target_from_property ?target_from_entity ;
        ?target_into_property ?target_into_entity ;
        sn:volume ?target_volume .

    # resulting volumes
    ?target_volume a sn:Volume ;
        sn:time ?time ;
        sn:unit ?unit ;
        sn:quantity ?aggregatedQuantity .
} WHERE {
    {
        SELECT  ?target_supplyNetwork ?target_supplylink ?target_volume
        # ?associatedEntity_property ?associatedEntity_from_entity ?associatedEntity_into_entity
        ?target_from_entity ?target_from_entitylabel ?target_into_entity ?target_into_entitylabel
        ?target_from_property ?target_into_property ?target_class ?target_supplyLinkKey ?target_supplyNetworkStructure
        ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
        WHERE {
            # identify supply network
            ?multidim_supplyNetwork a sn:SupplyNetwork ;
                sn:structure ?multidim_supplyNetworkStructure ;
                sn:supplyLink ?supplylink .

            # identify dimensions in tbox
            ?multidim_supplyNetworkStructure a qb:DataStructureDefinition ;
                qb:component/qb:dimension ?dimA_from_property ;
                qb:component/qb:dimension ?dimA_into_property ;
                qb:component/qb:dimension ?dimB_from_property ;
                qb:component/qb:dimension ?dimB_into_property ;
                qb:sliceKey ?multidim_supplyLinkKey .
            ?dimA_from_property rdfs:subPropertyOf sn:from .
            ?dimA_into_property rdfs:subPropertyOf sn:into .
            ?dimB_from_property rdfs:subPropertyOf sn:from .
            ?dimB_into_property rdfs:subPropertyOf sn:into .
            FILTER (?dimA_from_property != ?dimB_from_property)
            FILTER (?dimA_into_property != ?dimB_into_property)
            ?multidim_supplyLinkKey a sn:SupplyLinkKey ;
                qb:componentProperty ?dimA_from_property , ?dimA_into_property , ?dimB_from_property , ?dimB_into_property .
            ?target_supplyLinkKey a sn:SupplyLinkKey ;
                qb:componentProperty ?dimA_from_property , ?dimA_into_property .
            ?target_supplyNetworkStructure a qb:DataStructureDefinition ;
                qb:component/qb:dimension ?dimA_from_property ;
                qb:component/qb:dimension ?dimA_into_property ;
                qb:sliceKey ?target_supplyLinkKey .
            FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?dimA_from_property, ?dimA_into_property, sn:time))}

            # identify the entities for all dimensions in abox
            ?supplylink a sn:SupplyLink ;
                ?dimA_from_property ?dimA_from_entity ;
                ?dimA_into_property ?dimA_into_entity ;
                ?dimB_from_property ?dimB_from_entity ;
                ?dimB_into_property ?dimB_into_entity ;
                sn:supplyLinkKey ?multidim_supplyLinkKey ;
                sn:volume ?volume .
            BIND (?dimA_from_property AS ?target_from_property) .
            BIND (?dimA_into_property AS ?target_into_property) .
            ?dimA_from_entity a ?dimA_class .
            ?dimA_into_entity a ?dimA_class .
            BIND (?dimA_from_entity AS ?target_from_entity) .
            BIND (?dimA_into_entity AS ?target_into_entity) .
            ?target_from_entity a ?target_class ;
                rdfs:label ?target_from_entitylabel .
            ?target_into_entity a ?target_class ;
                    rdfs:label ?target_into_entitylabel .
            # OPTIONAL {
            #     ?dimA_from_entity ?associatedEntity_property ?associatedEntity_from_entity .
            #     ?dimA_into_entity ?associatedEntity_property ?associatedEntity_into_entity .
            #     ?associatedEntity_property rdfs:subPropertyOf sn:associatedEntity .
            # }
            ?dimB_from_entity a ?dimB_class .
            ?dimB_into_entity a ?dimB_class .

            # varify classes in tbox
            ?dimA_class rdfs:subClassOf sn:Entity .
            ?dimB_class rdfs:subClassOf sn:Entity .
            ?target_class rdfs:subClassOf sn:Entity .

            # identify volumes in abox
            ?volume a sn:Volume ;
                sn:time ?time ;
                sn:unit ?unit ;
                sn:quantity ?quantity .

            # coin uris
            BIND(IRI(CONCAT("https://www.example.org/",MD5(CONCAT(STR(?multidim_supplyNetwork),STR(?target_supplyNetworkStructure))))) AS ?target_supplyNetwork) .
            BIND(IRI(CONCAT("https://www.example.org/",MD5(CONCAT(STR(?target_from_entity),STR(?target_into_entity))))) AS ?target_supplylink) .
            BIND(IRI(CONCAT("https://www.example.org/",MD5(CONCAT(STR(?target_supplylink),STR(?time),STR(?unit))))) AS ?target_volume) .
        }
        GROUP BY ?target_supplyNetwork ?target_supplylink ?target_volume
        # ?associatedEntity_property ?associatedEntity_from_entity ?associatedEntity_into_entity
        ?target_from_entity ?target_from_entitylabel ?target_into_entity ?target_into_entitylabel
        ?target_from_property ?target_into_property ?target_class ?target_supplyLinkKey ?target_supplyNetworkStructure
        ?time ?unit
    }
}
