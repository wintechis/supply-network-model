BASE <https://purl.org/supply-network/any-data/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sn: <https://purl.org/supply-network/onto#>
PREFIX qb:    <http://purl.org/linked-data/cube#>

CONSTRUCT {
    # resulting supply network
    ?target_supplyNetwork a sn:SupplyNetwork ;
        sn:structure ?target_supplyNetworkStructure ;
        sn:supplyFlow ?target_supplyflow ;
        sn:inferredFrom ?source_supplyNetwork .

    # resulting entities
    ?target_dimA_from_entity a ?target_dimA_class ;
        rdfs:label ?target_dimA_from_entitylabel .
    ?target_dimA_into_entity a ?target_dimA_class ;
        rdfs:label ?target_dimA_into_entitylabel .
    ?target_dimB_from_entity a ?target_dimB_class ;
        rdfs:label ?target_dimB_from_entitylabel .
    ?target_dimB_into_entity a ?target_dimB_class ;
        rdfs:label ?target_dimB_into_entitylabel .

    # resulting supply links
    ?target_supplyflow a sn:SupplyFlow ;
        sn:supplyFlowStructure ?target_supplyFlowKey ;
        ?target_dimA_from_property ?target_dimA_from_entity ;
        ?target_dimA_into_property ?target_dimA_into_entity ;
        ?target_dimB_from_property ?target_dimB_from_entity ;
        ?target_dimB_into_property ?target_dimB_into_entity ;
        sn:volume ?target_volume .

    # resulting volumes
    ?target_volume a sn:Volume ;
        sn:time ?time ;
        sn:quantity ?aggregatedQuantity ;
        sn:unit ?unit .
} WHERE {
    {
        SELECT ?source_supplyNetwork ?target_supplyNetwork ?target_supplyflow ?target_volume
        ?target_dimA_from_entity ?target_dimA_from_entitylabel ?target_dimA_into_entity ?target_dimA_into_entitylabel
        ?target_dimB_from_entity ?target_dimB_from_entitylabel ?target_dimB_into_entity ?target_dimB_into_entitylabel
        ?target_dimA_from_property ?target_dimA_into_property ?target_dimA_class
        ?target_dimB_from_property ?target_dimB_into_property ?target_dimB_class
        ?target_supplyFlowKey ?target_supplyNetworkStructure
        ?time ?unit (SUM(?quantity) AS ?aggregatedQuantity)
        WHERE {
            {
            # query part to infer single-dimension supply network from multi-dimension supply network
            # where the single-dimension is NOT part of the multi-dimension

                # identify target abstraction and associated entity property that links source and target
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:higherAbstractionLevel ?target_supplyNetworkStructure ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property .
                ?source_dimA_from_property rdfs:range ?source_class .
                ?source_dimA_into_property rdfs:range ?source_class .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?target_dimA_from_property ;
                    qb:component/qb:dimension ?target_dimA_into_property .
                ?target_dimA_from_property rdfs:range ?target_dimA_class .
                ?target_dimA_into_property rdfs:range ?target_dimA_class .
                ?abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_class ;
                    rdfs:range ?target_dimA_class .

                # identify source dimensions in tbox
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?source_dimB_from_property ;
                    qb:component/qb:dimension ?source_dimB_into_property ;
                    sn:supplyFlowKey ?source_supplyFlowKey .
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .
                ?source_dimB_from_property rdfs:subPropertyOf sn:from .
                ?source_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER (?source_dimA_from_property != ?source_dimB_from_property)
                FILTER (?source_dimA_into_property != ?source_dimB_into_property)
                ?source_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property , ?source_dimB_from_property , ?source_dimB_into_property .

                # identify target dimensions in tbox
                ?target_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?target_dimA_from_property , ?target_dimA_into_property .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?target_supplyFlowKey .
                ?target_dimA_from_property rdfs:subPropertyOf sn:from .
                ?target_dimA_into_property rdfs:subPropertyOf sn:into .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?target_dimA_from_property, ?target_dimA_into_property, sn:time))}

                # identify the source entities for all dimensions in abox
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    ?source_dimB_from_property ?source_dimB_from_entity ;
                    ?source_dimB_into_property ?source_dimB_into_entity ;
                    sn:supplyFlowStructure ?source_supplyFlowKey ;
                    sn:volume ?volume .
                ?source_dimA_from_entity a ?source_dimA_class .
                ?source_dimA_into_entity a ?source_dimA_class .
                ?source_dimB_from_entity a ?source_dimB_class .
                ?source_dimB_into_entity a ?source_dimB_class .

                # identify the target entities for all dimensions in abox
                ?source_dimA_from_entity ?abstractSubstitute_property ?target_dimA_from_entity .
                ?source_dimA_into_entity ?abstractSubstitute_property ?target_dimA_into_entity .

                # varify classes in tbox
                ?source_dimB_class rdfs:subClassOf sn:Entity .

            } UNION {
            # query part to infer single-dimension supply network from multi-dimension supply network
            # where the single-dimension IS part of the multi-dimension

                # identify dimensions in tbox
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property ;
                    qb:component/qb:dimension ?source_dimB_from_property ;
                    qb:component/qb:dimension ?source_dimB_into_property ;
                    sn:supplyFlowKey ?source_supplyFlowKey .
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .
                ?source_dimB_from_property rdfs:subPropertyOf sn:from .
                ?source_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER (?source_dimA_from_property != ?source_dimB_from_property)
                FILTER (?source_dimA_into_property != ?source_dimB_into_property)
                ?source_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property , ?source_dimB_from_property , ?source_dimB_into_property .
                ?target_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property ;
                    sn:supplyFlowKey ?target_supplyFlowKey .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?source_dimA_from_property, ?source_dimA_into_property, sn:time))}

                # identify the entities for all dimensions in abox
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    ?source_dimB_from_property ?source_dimB_from_entity ;
                    ?source_dimB_into_property ?source_dimB_into_entity ;
                    sn:supplyFlowStructure ?source_supplyFlowKey ;
                    sn:volume ?volume .
                BIND (?source_dimA_from_property AS ?target_dimA_from_property) .
                BIND (?source_dimA_into_property AS ?target_dimA_into_property) .
                ?source_dimA_from_entity a ?source_dimA_class .
                ?source_dimA_into_entity a ?source_dimA_class .
                BIND (?source_dimA_from_entity AS ?target_dimA_from_entity) .
                BIND (?source_dimA_into_entity AS ?target_dimA_into_entity) .
                ?source_dimB_from_entity a ?source_dimB_class .
                ?source_dimB_into_entity a ?source_dimB_class .

                # varify classes in tbox
                ?source_dimB_class rdfs:subClassOf sn:Entity .

            } UNION {
            # query part to infer single-dimension supply network from single-dimension supply network
                
                # identify target abstraction in t-box
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:higherAbstractionLevel ?target_supplyNetworkStructure ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property .
    			FILTER NOT EXISTS { ?source_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?source_dimA_from_property, ?source_dimA_into_property, sn:time)) }
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?target_dimA_from_property ;
                    qb:component/qb:dimension ?target_dimA_into_property .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?target_dimA_from_property, ?target_dimA_into_property, sn:time)) }

                # identify abstract substitute subproperty that links source and target in t-box
                ?source_dimA_from_property rdfs:range ?source_dimA_class .
                ?source_dimA_into_property rdfs:range ?source_dimA_class .
                ?target_dimA_from_property rdfs:range ?target_dimA_class .
                ?target_dimA_into_property rdfs:range ?target_dimA_class .
                ?abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_dimA_class ;
                    rdfs:range ?target_dimA_class .

                # identify source dimensions properties in t-box
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .

                # identify target dimensions properties in t-box
                ?target_dimA_from_property rdfs:subPropertyOf sn:from .
                ?target_dimA_into_property rdfs:subPropertyOf sn:into .

                # identify source entities in a-box
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    sn:volume ?volume .

                # identify target entities in a-box
                ?source_dimA_from_entity ?abstractSubstitute_property ?target_dimA_from_entity .
                ?source_dimA_into_entity ?abstractSubstitute_property ?target_dimA_into_entity .

            } UNION {
            # query part to infer multi-dimension supply network from single-dimension supply network
                
                # identify target abstraction and associated entity property that links source and target
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:higherAbstractionLevel ?target_supplyNetworkStructure ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property .
    			FILTER NOT EXISTS { ?source_supplyNetworkStructure qb:component/qb:dimension ?dim .FILTER (?dim NOT IN (?source_dimA_from_property, ?source_dimA_into_property, sn:time))}
                ?source_dimA_from_property rdfs:range ?source_class .
                ?source_dimA_into_property rdfs:range ?source_class .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?target_dimA_from_property ;
                    qb:component/qb:dimension ?target_dimA_into_property ;
                    qb:component/qb:dimension ?target_dimB_from_property ;
                    qb:component/qb:dimension ?target_dimB_into_property .
                ?target_dimA_from_property rdfs:range ?target_dimA_class .
                ?target_dimA_into_property rdfs:range ?target_dimA_class .
                ?target_dimB_from_property rdfs:range ?target_dimB_class .
                ?target_dimB_into_property rdfs:range ?target_dimB_class .
                ?dimA_abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_class ;
                    rdfs:range ?target_dimA_class .
                ?dimB_abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_class ;
                    rdfs:range ?target_dimB_class .
                FILTER (?dimA_abstractSubstitute_property != ?dimB_abstractSubstitute_property)
                FILTER (?target_dimA_class != ?target_dimB_class)

                # identify source dimensions in tbox
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?source_supplyFlowKey .
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .
                ?source_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property .

                # identify target dimensions in tbox
                ?target_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?target_dimA_from_property , ?target_dimA_into_property , ?target_dimB_from_property , ?target_dimB_into_property .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?target_supplyFlowKey .
                ?target_dimA_from_property rdfs:subPropertyOf sn:from .
                ?target_dimA_into_property rdfs:subPropertyOf sn:into .
                ?target_dimB_from_property rdfs:subPropertyOf sn:from .
                ?target_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?target_dimA_from_property, ?target_dimA_into_property, ?target_dimB_from_property, ?target_dimB_into_property, sn:time))}

                # identify the source entities for all dimensions in abox
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    sn:supplyFlowStructure ?source_supplyFlowKey ;
                    sn:volume ?volume .
                ?source_dimA_from_entity a ?source_dimA_class .
                ?source_dimA_into_entity a ?source_dimA_class .

                # identify the target entities for all dimensions in abox
                ?source_dimA_from_entity ?dimA_abstractSubstitute_property ?target_dimA_from_entity .
                ?source_dimA_into_entity ?dimA_abstractSubstitute_property ?target_dimA_into_entity .
                ?source_dimA_from_entity ?dimB_abstractSubstitute_property ?target_dimB_from_entity .
                ?source_dimA_into_entity ?dimB_abstractSubstitute_property ?target_dimB_into_entity .

                # varify classes in tbox
                ?target_dimB_class rdfs:subClassOf sn:Entity .

                # identify labels and class of entities in abox
                ?target_dimB_from_entity rdfs:label ?target_dimB_from_entitylabel .
                ?target_dimB_into_entity rdfs:label ?target_dimB_into_entitylabel .

            } UNION {
            # query part to infer multi-dimension supply network from multi-dimension supply network
            # where ONE of the target dimensions is part of the source multi-dimension
                
                # identify target abstraction and associated entity property that links source and target
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:higherAbstractionLevel ?target_supplyNetworkStructure ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property .
                ?source_dimA_from_property rdfs:range ?source_class .
                ?source_dimA_into_property rdfs:range ?source_class .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?target_dimA_from_property ;
                    qb:component/qb:dimension ?target_dimA_into_property ;
                    qb:component/qb:dimension ?source_dimB_from_property ;
                    qb:component/qb:dimension ?source_dimB_into_property .
                ?target_dimA_from_property rdfs:range ?target_dimA_class .
                ?target_dimA_into_property rdfs:range ?target_dimA_class .
                ?abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_class ;
                    rdfs:range ?target_dimA_class .

                # identify source dimensions in tbox
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?source_dimB_from_property ;
                    qb:component/qb:dimension ?source_dimB_into_property ;
                    sn:supplyFlowKey ?source_supplyFlowKey .
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .
                ?source_dimB_from_property rdfs:subPropertyOf sn:from .
                ?source_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER (?source_dimA_from_property != ?source_dimB_from_property)
                FILTER (?source_dimA_into_property != ?source_dimB_into_property)
                FILTER (?target_dimA_from_property != ?source_dimB_from_property)
                FILTER (?target_dimA_into_property != ?source_dimB_into_property)
                ?source_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property , ?source_dimB_from_property , ?source_dimB_into_property .

                # identify target dimensions in tbox
                ?target_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?target_dimA_from_property , ?target_dimA_into_property , ?source_dimB_from_property , ?source_dimB_into_property .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?target_supplyFlowKey .
                ?target_dimA_from_property rdfs:subPropertyOf sn:from .
                ?target_dimA_into_property rdfs:subPropertyOf sn:into .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?target_dimA_from_property, ?target_dimA_into_property, ?source_dimB_from_property, ?source_dimB_into_property, sn:time))}

                # identify the source entities for all dimensions in abox
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    ?source_dimB_from_property ?source_dimB_from_entity ;
                    ?source_dimB_into_property ?source_dimB_into_entity ;
                    sn:supplyFlowStructure ?source_supplyFlowKey ;
                    sn:volume ?volume .
                ?source_dimA_from_entity a ?source_dimA_class .
                ?source_dimA_into_entity a ?source_dimA_class .
                ?source_dimB_from_entity a ?source_dimB_class .
                ?source_dimB_into_entity a ?source_dimB_class .

                # identify the target entities for all dimensions in abox
                ?source_dimA_from_entity ?abstractSubstitute_property ?target_dimA_from_entity .
                ?source_dimA_into_entity ?abstractSubstitute_property ?target_dimA_into_entity .
                BIND (?source_dimB_from_property AS ?target_dimB_from_property) .
                BIND (?source_dimB_into_property AS ?target_dimB_into_property) .
                BIND (?source_dimB_from_entity AS ?target_dimB_from_entity) .
                BIND (?source_dimB_into_entity AS ?target_dimB_into_entity) .

                # varify classes in tbox
                ?source_dimB_class rdfs:subClassOf sn:Entity .
                ?target_dimB_class rdfs:subClassOf sn:Entity .

                # identify labels and class of entities in abox
                ?target_dimB_from_entity rdfs:label ?target_dimB_from_entitylabel .
                ?target_dimB_into_entity rdfs:label ?target_dimB_into_entitylabel .

            } UNION {
            # query part to infer multi-dimension supply network from multi-dimension supply network
            # where NONE of the target dimensions is part of the source multi-dimension
            # and both target dimensions infer from THE SAME source dimension
                
                # identify target abstraction and associated entity property that links source and target
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:higherAbstractionLevel ?target_supplyNetworkStructure ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property ;
                    qb:component/qb:dimension ?source_dimB_from_property ;
                    qb:component/qb:dimension ?source_dimB_into_property .
                ?source_dimA_from_property rdfs:range ?source_dimA_class .
                ?source_dimA_into_property rdfs:range ?source_dimA_class .
                ?source_dimB_from_property rdfs:range ?source_dimB_class .
                ?source_dimB_into_property rdfs:range ?source_dimB_class .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?target_dimA_from_property ;
                    qb:component/qb:dimension ?target_dimA_into_property ;
                    qb:component/qb:dimension ?target_dimB_from_property ;
                    qb:component/qb:dimension ?target_dimB_into_property .
                ?target_dimA_from_property rdfs:range ?target_dimA_class .
                ?target_dimA_into_property rdfs:range ?target_dimA_class .
                ?target_dimB_from_property rdfs:range ?target_dimB_class .
                ?target_dimB_into_property rdfs:range ?target_dimB_class .
                ?dimA_abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_dimA_class ;
                    rdfs:range ?target_dimA_class .
                ?dimB_abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_dimA_class ;
                    rdfs:range ?target_dimB_class .
                FILTER (?dimA_abstractSubstitute_property != ?dimB_abstractSubstitute_property)

                # identify source dimensions in tbox
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?source_supplyFlowKey .
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .
                ?source_dimB_from_property rdfs:subPropertyOf sn:from .
                ?source_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER (?source_dimA_from_property != ?source_dimB_from_property)
                FILTER (?source_dimA_into_property != ?source_dimB_into_property)
                FILTER (?target_dimA_from_property != ?target_dimB_from_property)
                FILTER (?target_dimA_into_property != ?target_dimB_into_property)
                ?source_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property , ?source_dimB_from_property , ?source_dimB_into_property .

                # identify target dimensions in tbox
                ?target_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?target_dimA_from_property , ?target_dimA_into_property , ?target_dimB_from_property , ?target_dimB_into_property .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?target_supplyFlowKey .
                ?target_dimA_from_property rdfs:subPropertyOf sn:from .
                ?target_dimA_into_property rdfs:subPropertyOf sn:into .
                ?target_dimB_from_property rdfs:subPropertyOf sn:from .
                ?target_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?target_dimA_from_property, ?target_dimA_into_property, ?target_dimB_from_property, ?target_dimB_into_property, sn:time))}

                # identify the source entities for all dimensions in abox
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    ?source_dimB_from_property ?source_dimB_from_entity ;
                    ?source_dimB_into_property ?source_dimB_into_entity ;
                    sn:supplyFlowStructure ?source_supplyFlowKey ;
                    sn:volume ?volume .
                ?source_dimA_from_entity a ?source_dimA_class .
                ?source_dimA_into_entity a ?source_dimA_class .
                ?source_dimB_from_entity a ?source_dimB_class .
                ?source_dimB_into_entity a ?source_dimB_class .

                # identify the target entities for all dimensions in abox
                ?source_dimA_from_entity ?dimA_abstractSubstitute_property ?target_dimA_from_entity .
                ?source_dimA_into_entity ?dimA_abstractSubstitute_property ?target_dimA_into_entity .
                ?source_dimA_from_entity ?dimB_abstractSubstitute_property ?target_dimB_from_entity .
                ?source_dimA_into_entity ?dimB_abstractSubstitute_property ?target_dimB_into_entity .

                # varify classes in tbox
                ?source_dimB_class rdfs:subClassOf sn:Entity .
                ?target_dimB_class rdfs:subClassOf sn:Entity .

                # identify labels and class of entities in abox
                ?target_dimB_from_entity rdfs:label ?target_dimB_from_entitylabel .
                ?target_dimB_into_entity rdfs:label ?target_dimB_into_entitylabel .

            } UNION {
            # query part to infer multi-dimension supply network from multi-dimension supply network
            # where NONE of the target dimensions is part of the source multi-dimension
            # and both target dimensions infer from TWO DIFFERENT source dimensions
                
                # identify target abstraction and associated entity property that links source and target
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:higherAbstractionLevel ?target_supplyNetworkStructure ;
                    qb:component/qb:dimension ?source_dimA_from_property ;
                    qb:component/qb:dimension ?source_dimA_into_property ;
                    qb:component/qb:dimension ?source_dimB_from_property ;
                    qb:component/qb:dimension ?source_dimB_into_property .
                ?source_dimA_from_property rdfs:range ?source_dimA_class .
                ?source_dimA_into_property rdfs:range ?source_dimA_class .
                ?source_dimB_from_property rdfs:range ?source_dimB_class .
                ?source_dimB_into_property rdfs:range ?source_dimB_class .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    qb:component/qb:dimension ?target_dimA_from_property ;
                    qb:component/qb:dimension ?target_dimA_into_property ;
                    qb:component/qb:dimension ?target_dimB_from_property ;
                    qb:component/qb:dimension ?target_dimB_into_property .
                ?target_dimA_from_property rdfs:range ?target_dimA_class .
                ?target_dimA_into_property rdfs:range ?target_dimA_class .
                ?target_dimB_from_property rdfs:range ?target_dimB_class .
                ?target_dimB_into_property rdfs:range ?target_dimB_class .
                ?dimA_abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_dimA_class ;
                    rdfs:range ?target_dimA_class .
                ?dimB_abstractSubstitute_property rdfs:subPropertyOf sn:abstractSubstitute ;
                    rdfs:domain ?source_dimB_class ; # this line changed
                    rdfs:range ?target_dimB_class .
                FILTER (?dimA_abstractSubstitute_property != ?dimB_abstractSubstitute_property)

                # identify source dimensions in tbox
                ?source_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?source_supplyFlowKey .
                ?source_dimA_from_property rdfs:subPropertyOf sn:from .
                ?source_dimA_into_property rdfs:subPropertyOf sn:into .
                ?source_dimB_from_property rdfs:subPropertyOf sn:from .
                ?source_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER (?source_dimA_from_property != ?source_dimB_from_property)
                FILTER (?source_dimA_into_property != ?source_dimB_into_property)
                FILTER (?target_dimA_from_property != ?target_dimB_from_property)
                FILTER (?target_dimA_into_property != ?target_dimB_into_property)
                ?source_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?source_dimA_from_property , ?source_dimA_into_property , ?source_dimB_from_property , ?source_dimB_into_property .

                # identify target dimensions in tbox
                ?target_supplyFlowKey a sn:SupplyFlowKey ;
                    qb:componentProperty ?target_dimA_from_property , ?target_dimA_into_property , ?target_dimB_from_property , ?target_dimB_into_property .
                ?target_supplyNetworkStructure a sn:Abstraction ;
                    sn:supplyFlowKey ?target_supplyFlowKey .
                ?target_dimA_from_property rdfs:subPropertyOf sn:from .
                ?target_dimA_into_property rdfs:subPropertyOf sn:into .
                ?target_dimB_from_property rdfs:subPropertyOf sn:from .
                ?target_dimB_into_property rdfs:subPropertyOf sn:into .
                FILTER NOT EXISTS { ?target_supplyNetworkStructure qb:component/qb:dimension ?dim . FILTER (?dim NOT IN (?target_dimA_from_property, ?target_dimA_into_property, ?target_dimB_from_property, ?target_dimB_into_property, sn:time))}

                # identify the source entities for all dimensions in abox
                ?source_supplyflow a sn:SupplyFlow ;
                    ?source_dimA_from_property ?source_dimA_from_entity ;
                    ?source_dimA_into_property ?source_dimA_into_entity ;
                    ?source_dimB_from_property ?source_dimB_from_entity ;
                    ?source_dimB_into_property ?source_dimB_into_entity ;
                    sn:supplyFlowStructure ?source_supplyFlowKey ;
                    sn:volume ?volume .
                ?source_dimA_from_entity a ?source_dimA_class .
                ?source_dimA_into_entity a ?source_dimA_class .
                ?source_dimB_from_entity a ?source_dimB_class .
                ?source_dimB_into_entity a ?source_dimB_class .

                # identify the target entities for all dimensions in abox
                ?source_dimA_from_entity ?dimA_abstractSubstitute_property ?target_dimA_from_entity .
                ?source_dimA_into_entity ?dimA_abstractSubstitute_property ?target_dimA_into_entity .
                ?source_dimB_from_entity ?dimB_abstractSubstitute_property ?target_dimB_from_entity . # this line changed
                ?source_dimB_into_entity ?dimB_abstractSubstitute_property ?target_dimB_into_entity . # this line changed

                # varify classes in tbox
                ?source_dimB_class rdfs:subClassOf sn:Entity .
                ?target_dimB_class rdfs:subClassOf sn:Entity .

                # identify labels and class of entities in abox
                ?target_dimB_from_entity rdfs:label ?target_dimB_from_entitylabel .
                ?target_dimB_into_entity rdfs:label ?target_dimB_into_entitylabel .

            }

            
            # identify source supply network
            ?source_supplyNetwork a sn:SupplyNetwork ;
                sn:structure ?source_supplyNetworkStructure ;
                sn:supplyFlow ?source_supplyflow .

            # identify labels of target entities in abox
            OPTIONAL { ?target_dimA_from_entity rdfs:label ?target_dimA_from_entitylabel . }
            OPTIONAL { ?target_dimA_into_entity rdfs:label ?target_dimA_into_entitylabel . }

            # identify volumes in abox
            ?volume a sn:Volume ;
                sn:time ?time ;
                sn:quantity ?quantity ;
                sn:unit ?unit .

            # coin uris
            BIND(IRI(MD5(CONCAT(STR(?source_supplyNetwork),STR(?target_supplyNetworkStructure)))) AS ?target_supplyNetwork) .
            BIND(COALESCE(?target_dimB_from_entity, <https://www.dummy.org>) AS ?target_dimB_from_id) . # always have a binding to coin the uri
            BIND(COALESCE(?target_dimB_into_entity, <https://www.dummy.org>) AS ?target_dimB_into_id) .
            BIND(IF(STR(?target_dimA_from_entity) < STR(?target_dimB_from_id), ?target_dimA_from_entity, ?target_dimB_from_id) AS ?target_first_from_entity) . # ensure consistent order so we dont get two uris for each pair
            BIND(IF(STR(?target_dimA_from_entity) < STR(?target_dimB_from_id), ?target_dimB_from_id, ?target_dimA_from_entity) AS ?target_second_from_entity) .
            BIND(IF(STR(?target_dimA_into_entity) < STR(?target_dimB_into_id), ?target_dimA_into_entity, ?target_dimB_into_id) AS ?target_first_into_entity) .
            BIND(IF(STR(?target_dimA_into_entity) < STR(?target_dimB_into_id), ?target_dimB_into_id, ?target_dimA_into_entity) AS ?target_second_into_entity) .
            BIND(IRI(MD5(CONCAT(STR(?target_first_from_entity),STR(?target_first_into_entity),STR(?target_second_from_entity),STR(?target_second_into_entity)))) AS ?target_supplyflow) .
            BIND(IRI(MD5(CONCAT(STR(?target_supplyflow),STR(?time),STR(?unit)))) AS ?target_volume) .
            
        }
        GROUP BY ?source_supplyNetwork ?target_supplyNetwork ?target_supplyflow ?target_volume
        ?target_dimA_from_entity ?target_dimA_from_entitylabel ?target_dimA_into_entity ?target_dimA_into_entitylabel
        ?target_dimB_from_entity ?target_dimB_from_entitylabel ?target_dimB_into_entity ?target_dimB_into_entitylabel
        ?target_dimA_from_property ?target_dimA_into_property ?target_dimA_class
        ?target_dimB_from_property ?target_dimB_into_property ?target_dimB_class
        ?target_supplyFlowKey ?target_supplyNetworkStructure
        ?time ?unit 
    }
}
