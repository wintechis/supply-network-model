PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sn: <https://purl.org/supply-network/onto#>

INSERT {
    ?supplyflow ?higherlevelInto ?higherLevelIntoEntity ;
        ?higherlevelFrom ?higherLevelFromEntity .
} WHERE {
    ?supplyflow rdf:type sn:SupplyFlow ;
        ?lowerlevelInto ?lowerLevelIntoEntity ;
        ?lowerlevelFrom ?lowerLevelFromEntity .
    ?lowerlevelInto rdfs:subPropertyOf sn:into .
    ?lowerlevelFrom rdfs:subPropertyOf sn:from .
    ?lowerLevelIntoEntity ?abstractSubstitute ?higherLevelIntoEntity .
    ?lowerLevelFromEntity ?abstractSubstitute ?higherLevelFromEntity .
    ?abstractSubstitute rdfs:subPropertyOf sn:abstractSubstitute ;
        rdfs:range ?higherlevelClass .
    ?higherlevelInto rdfs:subPropertyOf sn:into ;
        rdfs:range ?higherlevelClass .
    ?higherlevelFrom rdfs:subPropertyOf sn:from ;
        rdfs:range ?higherlevelClass .
}
